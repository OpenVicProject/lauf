module @call;

function @produce_value(0 => 1) {
    uint 11;
    return;
}
function @produce_values(0 => 3) {
    uint 11;
    uint 42;
    uint 66;
    return;
}

function @identity(1 => 1) {
    return;
}
function @replace_value(1 => 1) {
    pop 0;
    uint 11;
    return;
}

function @consume_value(1 => 0) {
    pop 0;
    return;
}

function @consume_and_produce_values(1 => 2) {
    uint 42;
    return;
}

function @call_invalid_addr(0 => 0) {
    uint 42; call_indirect (0 => 0);
    return;
}
function @call_wrong_signature(0 => 0) {
    function_addr @identity; call_indirect (0 => 0);
    return;
}

function @vstack_overflow() {
    null; null; null; null;
    null; null; null; null;
    null; null; null; null;
    null; null; null; null;

    function_addr @vstack_overflow; call_indirect ();

    pop 0; pop 0; pop 0; pop 0;
    pop 0; pop 0; pop 0; pop 0;
    pop 0; pop 0; pop 0; pop 0;
    pop 0; pop 0; pop 0; pop 0;
    return;
}
function @cstack_overflow() {
    function_addr @cstack_overflow; call_indirect ();
    return;
}
function @cstack_overflow_local() {
    local %local : (1024, 8);
    function_addr @cstack_overflow; call_indirect ();
    return;
}

global const @msg_vstack_overflow = "vstack overflow", 0;
global const @msg_cstack_overflow = "cstack overflow", 0;
global const @msg_invalid_addr = "invalid function address", 0;

function @main() {
    [
        function_addr @produce_value; call_indirect (0 => 1);
        uint 11; $lauf.test.assert_eq;
    ]
    [
        function_addr @produce_values; call_indirect (0 => 3);
        uint 66; $lauf.test.assert_eq;
        uint 42; $lauf.test.assert_eq;
        uint 11; $lauf.test.assert_eq;
    ]

    [
        uint 11;
        function_addr @identity; call_indirect (1 => 1);
        uint 11; $lauf.test.assert_eq;
    ]
    [
        uint 42;
        function_addr @replace_value; call_indirect (1 => 1);
        uint 11; $lauf.test.assert_eq;
    ]

    [
        uint 11;
        uint 42;
        function_addr @consume_value; call_indirect (1 => 0);
        uint 11; $lauf.test.assert_eq;
    ]

    [
        uint 11;
        function_addr @consume_and_produce_values; call_indirect (1 => 2);
        uint 42; $lauf.test.assert_eq;
        uint 11; $lauf.test.assert_eq;
    ]

    [
        function_addr @call_invalid_addr; global_addr @msg_invalid_addr; $lauf.test.assert_panic;
        function_addr @call_wrong_signature; global_addr @msg_invalid_addr; $lauf.test.assert_panic;

        function_addr @vstack_overflow; global_addr @msg_vstack_overflow; $lauf.test.assert_panic;
        function_addr @cstack_overflow; global_addr @msg_cstack_overflow; $lauf.test.assert_panic;
        function_addr @cstack_overflow_local; global_addr @msg_cstack_overflow; $lauf.test.assert_panic;
    ]

    return;
}

