module @call;

function @produce_value(0 => 1) {
    uint 11;
    return;
}
function @produce_values(0 => 3) {
    uint 11;
    uint 42;
    uint 66;
    return;
}

function @identity(1 => 1) {
    return;
}
function @replace_value(1 => 1) {
    pop 0;
    uint 11;
    return;
}

function @consume_value(1 => 0) {
    pop 0;
    return;
}

function @consume_and_produce_values(1 => 2) {
    uint 42;
    return;
}

function @vstack_overflow() {
    null; null; null; null;
    null; null; null; null;
    null; null; null; null;
    null; null; null; null;

    call @vstack_overflow;

    pop 0; pop 0; pop 0; pop 0;
    pop 0; pop 0; pop 0; pop 0;
    pop 0; pop 0; pop 0; pop 0;
    pop 0; pop 0; pop 0; pop 0;
    return;
}
function @cstack_overflow() {
    call @cstack_overflow;
    return;
}
function @cstack_overflow_local() {
    local %local : (1024, 8);
    call @cstack_overflow_local();
    return;
}

global const @msg_vstack_overflow = "vstack overflow", 0;
global const @msg_cstack_overflow = "cstack overflow", 0;

function @main(0 => 1) export {
    [
        call @produce_value;
        uint 11; $lauf.test.assert_eq;
    ]
    [
        call @produce_values;
        uint 66; $lauf.test.assert_eq;
        uint 42; $lauf.test.assert_eq;
        uint 11; $lauf.test.assert_eq;
    ]

    [
        uint 11;
        call @identity;
        uint 11; $lauf.test.assert_eq;
    ]
    [
        uint 42;
        call @replace_value;
        uint 11; $lauf.test.assert_eq;
    ]

    [
        uint 11;
        uint 42;
        call @consume_value;
        uint 11; $lauf.test.assert_eq;
    ]

    [
        uint 11;
        call @consume_and_produce_values;
        uint 42; $lauf.test.assert_eq;
        uint 11; $lauf.test.assert_eq;
    ]

    [
        function_addr @vstack_overflow; global_addr @msg_vstack_overflow; $lauf.test.assert_panic;
        function_addr @cstack_overflow; global_addr @msg_cstack_overflow; $lauf.test.assert_panic;
        function_addr @cstack_overflow_local; global_addr @msg_cstack_overflow; $lauf.test.assert_panic;
    ]

    uint 0; return;
}

